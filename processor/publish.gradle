apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = rootProject.ext.groupName
version = rootProject.ext.libraryVersionName

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact sourcesJar

            groupId rootProject.ext.groupName
            artifactId rootProject.ext.compilerArtifactId
            version rootProject.ext.libraryVersionName

            pom {
                name = libraryName
                description = rootProject.ext.packageDesc
                url = rootProject.ext.githubUrl
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'sabgilhun'
                        name = 'Yong Hun Kim'
                        email = 'sabgilhun@gamil.com'
                    }
                }
                scm {
                    connection = rootProject.ext.sourceUrl
                    developerConnection = rootProject.ext.sourceUrl
                    url = rootProject.ext.githubUrl
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    dryRun = false
    override = false
    publish = true

    pkg {
        repo = rootProject.ext.repoName
        name = rootProject.ext.compilerPackageName
        userOrg = user
        licenses = ['Apache-2.0']
        desc = rootProject.ext.packageDesc
        vcsUrl = rootProject.ext.sourceUrl

        version {
            name = rootProject.ext.libraryVersionName
            vcsTag = rootProject.ext.libraryVersionName
        }
    }

    publications = ['Production']
}